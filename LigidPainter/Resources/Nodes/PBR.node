# For ligidtools.com
# Node extension v1.0

%attributes;

-title : Image Texture;
-color : hex30c959; #rgb122,124,66  #hsv86,45,23
-opacity : 60;



#Albedo
-input_0 : 
--list : none;
--list_index : none;
--type : vec3;
--title : Albedo;
--element : color;

#Roughness
-input_1 : 
--list : none;
--list_index : none;
--type : float;
--title : Roughness;
--element : range;

#Metallic
-input_2 : 
--list : none;
--list_index : none;
--type : float;
--title : Metallic;
--element : range;

#Normal
-input_3 : 
--list : none;
--list_index : none;
--type : vec3;
--title : Normal;
--element : image;

#Ambient occlusion
-input_4 : 
--list : none;
--list_index : none;
--type : float;
--title : Ambient occlusion;
--element : range;

#Emission
-input_5 : 
--list : none;
--list_index : none;
--type : vec3;
--title : Emission;
--element : range;

#Emission Strength
-input_6 : 
--list : none;
--list_index : none;
--type : float;
--title : Emission Strength;
--element : range;

#Alpha
-input_7 : 
--list : none;
--list_index : none;
--type : float;
--title : Alpha;
--element : range;

-output_0 :
--list : none;
--list_index : none;
--type : vec3;
--title : Texture;


-uniforms : tex_coords|normal|posScene|posModel|viewPos|blurySkybox|prefilterMap|tangent|bitangent;

%code;

const float PI = 3.14159265359;


float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness*roughness;
    float a2 = a*a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}
// ----------------------------------------------------------------------------
float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}
// ----------------------------------------------------------------------------
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);
    float ggx1 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}
// ----------------------------------------------------------------------------
vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}
// ----------------------------------------------------------------------------
vec3 fresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness)
{
    return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}   
vec3 getRealisticResult(){
   vec3 albedo = texture2D(input_0,tex_coords).rgb;
   float roughness = texture2D(input_1,tex_coords).r;
   float metallic = texture2D(input_2,tex_coords).r;
   float ao = texture2D(input_4,tex_coords).r;

   
   vec3 lightPos1 = vec3(-10.0,  10.0, 10.0);
   vec3 lightPos2 = vec3( 10.0,  10.0, 10.0);
   vec3 lightPos3 = vec3(-10.0, -10.0, 10.0);
   vec3 lightPos4 = vec3( 10.0, -10.0, 10.0);

   vec3 lightColorX = vec3(300.0f, 300.0f, 300.0f);

    vec3 T = normalize(tangent);
    vec3 aN = normalize(normal);
    T = normalize(T - dot(T, aN) * aN);
    vec3 B = cross(aN, T);
    
    mat3 TBN = transpose(mat3(T, B, aN));    
    
    vec3 tangentViewPos  = TBN * viewPos;
    vec3 tangentPosModel  = TBN * posModel;

    vec3 N = texture(input_3, tex_coords).rgb;
    N = normalize(N * 2.0 - 1.0);  // this normal is in tangent space

    vec3 V = normalize(tangentViewPos - tangentPosModel);
    vec3 R = reflect(-V, N); 

    // calculate reflectance at normal incidence; if dia-electric (like plastic) use F0 
    // of 0.04 and if it's a metal, use the albedo color as F0 (metallic workflow)    
    vec3 F0 = vec3(0.04); 
    F0 = mix(F0, albedo, metallic);

    // reflectance equation
    vec3 Lo = vec3(0.0);
    for(int i = 0; i < 4; ++i) 
    {
      vec3 lightPosX;

      if(i == 0)
         lightPosX = lightPos1;
      if(i == 1)
         lightPosX = lightPos2;
      if(i == 2)
         lightPosX = lightPos3;
      if(i == 3)
         lightPosX = lightPos4;

         vec3 tangentLightPos = TBN * lightPosX;


        // calculate per-light radiance
        vec3 L = normalize(tangentLightPos - tangentPosModel);
        vec3 H = normalize(V + L);
        float distance = length(tangentLightPos - tangentPosModel);
        float attenuation = 1.0 / (distance * distance);
        vec3 radiance = lightColorX * attenuation;

        // Cook-Torrance BRDF
        float NDF = DistributionGGX(N, H, roughness);   
        float G   = GeometrySmith(N, V, L, roughness);    
        vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);        
        
        vec3 numerator    = NDF * G * F;
        float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.0001; // + 0.0001 to prevent divide by zero
        vec3 specular = numerator / denominator;
        
         // kS is equal to Fresnel
        vec3 kS = F;
        // for energy conservation, the diffuse and specular light can't
        // be above 1.0 (unless the surface emits light); to preserve this
        // relationship the diffuse component (kD) should equal 1.0 - kS.
        vec3 kD = vec3(1.0) - kS;
        // multiply kD by the inverse metalness such that only non-metals 
        // have diffuse lighting, or a linear blend if partly metal (pure metals
        // have no diffuse light).
        kD *= 1.0 - metallic;	                
            
        // scale light by NdotL
        float NdotL = max(dot(N, L), 0.0);        

        // add to outgoing radiance Lo
        Lo += (kD * albedo / PI + specular) * radiance * NdotL; // note that we already multiplied the BRDF by the Fresnel (kS) so we won't multiply by kS again
    }   
    
    // ambient lighting (we now use IBL as the ambient term)
    vec3 F = fresnelSchlickRoughness(max(dot(N, V), 0.0), F0, roughness);
    
    vec3 kS = F;
    vec3 kD = 1.0 - kS;
    kD *= 1.0 - metallic;	  
    
    vec3 irradiance = texture(blurySkybox, N).rgb;
    vec3 diffuse      = irradiance * albedo;
    
    // sample both the pre-filter map and the BRDF lut and combine them together as per the Split-Sum approximation to get the IBL specular part.
    const float MAX_REFLECTION_LOD = 4.0;
    vec3 prefilteredColor = textureLod(prefilterMap, R,  roughness * MAX_REFLECTION_LOD).rgb;    
    //vec2 brdf  = texture(brdfLUT, vec2(max(dot(N, V), 0.0), roughness)).rg;
    vec3 specular = prefilteredColor * (F);

    vec3 ambient = (kD * diffuse + specular) * ao;

   vec3 result = ambient + Lo;

   result = result / (result + vec3(1.0));

   result = pow(result,vec3(1.0/2.2));

   return result;
}

void main() {
    vec3 result = getRealisticResult();

    float alpha = texture2D(input_7,tex_coords).r;
    
    float emissionStr = texture2D(input_6,tex_coords).r;
    
    vec3 emission = texture2D(input_5,tex_coords).rgb;
    
    output_0 = vec4(mix(result,emission,emissionStr),alpha);
}